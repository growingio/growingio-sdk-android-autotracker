/*
 *   Copyright (C) 2020 Beijing Yishu Technology Co., Ltd.
 *
 *   Licensed under the Apache License, Version 2.0 (the "License");
 *   you may not use this file except in compliance with the License.
 *   You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 */

plugins {
	id 'java-platform'
	id 'maven-publish'
}

javaPlatform {
	allowDependencies()
}

dependencies {
	constraints {

		project.rootProject.subprojects.stream()
				.filter {
					!it.name.startsWith("demo") &&
							it.name != project.name &&
							it.name != "gio-sdk" &&
							it.name != "growingio-tools" &&
							it.name != "growingio-network" &&
							it.name != "growingio-webservice" &&
							it.name != "growingio-data" &&
							it.name != "growingio-adapter" &&
							it.name != "snappy" &&
							it.name != "gmonitor"
				}
				.map {
					it.name.replace("growingio-", "")
				}
				.forEach {
					api "com.growingio.android:" + it + ":" + libs.versions.growingio.get()
				}

		api "com.growingio.android:analytics-fa:4.0.0"
		api "com.growingio.android:analytics-sa:4.3.0"
		api "com.growingio.android:dummy-sa:4.3.0"

		// giokit
		api 'com.growingio.giokit:giokit:2.1.2'
		api 'com.growingio.giokit:giokit-no-op:2.1.2'

		// saas sdk
		//api 'com.growingio.android:vds-android-agent:autotrack-2.9.13'
		//api 'com.growingio.android:vds-android-agent:track-2.9.13'

		// tool
		//runtime "com.growingio.android:snappy:1.1.0"
		//runtime "com.growingio.android:gmonitor:2.0.0"
	}
}

apply from: "publishMaven.gradle"