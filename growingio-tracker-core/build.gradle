apply plugin: 'com.android.library'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim() as String
def buildTime = new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("GMT+8"))

android {
    compileSdkVersion buildConfiguration.compileVersion
    defaultConfig {
        minSdkVersion buildConfiguration.minSdkVersion
        targetSdkVersion buildConfiguration.targetSdkVersion
        versionName releaseConfiguration.releaseVersion
        versionCode releaseConfiguration.releaseVersionCode

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField("int", "VERSION_CODE", "${defaultConfig.versionCode}")
        buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'consumer-proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility buildConfiguration.sourceCompatibility
        targetCompatibility buildConfiguration.targetCompatibility
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {

    compileOnly fileTree(dir: '../thirdLibs', include: ['*.jar'])

    testImplementation libraries.test.junit
    testImplementation libraries.test.truth
    testImplementation libraries.test.androidx_core
    testImplementation libraries.test.robolectric
    testImplementation libraries.androidx.appcompat
    testImplementation libraries.androidx.recyclerview
    testImplementation libraries.androidx.swiperefreshlayout

    testImplementation project(path: ':growingio-data:database')
    testImplementation project(path: ':growingio-data:protobuf')
    testImplementation project(path: ':growingio-data:json')
    testImplementation libraries.google.protobuf_javalite

    testImplementation fileTree(dir: '../thirdLibs', include: ['*.jar'])

    compileOnly libraries.androidx.appcompat
    compileOnly libraries.androidx.recyclerview
    compileOnly libraries.androidx.swiperefreshlayout
    compileOnly libraries.android.appcompat
    compileOnly libraries.android.recyclerview
    //compileOnly libraries.others.x5_webview

}

apply from: "${rootProject.projectDir}/gradle/publishMaven.gradle"
apply from: "${rootProject.projectDir}/gradle/jacocoModule.gradle"
