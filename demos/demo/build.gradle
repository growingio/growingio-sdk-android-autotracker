apply plugin: 'com.android.application'
apply plugin: 'jacoco'

def withAutotrack = true

if (withAutotrack) {
    apply plugin: 'com.growingio.android.autotracker'
}

buildscript {
    repositories {
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath "com.growingio.android:autotracker-gradle-plugin:${releaseConfiguration.releaseVersion}"
    }
}

android {
    compileSdkVersion buildConfiguration.compileVersion
    defaultConfig {
        applicationId "com.gio.test.three"
        minSdkVersion buildConfiguration.minSdkVersion
        targetSdkVersion buildConfiguration.targetSdkVersion

        buildConfigField "boolean", "withAutotrack", "$withAutotrack"

        testInstrumentationRunner "com.growingio.autotest.TrackAndroidJUnitRunner"

        testInstrumentationRunnerArguments listener: "com.growingio.autotest.TrackTestRunListener"

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }
        multiDexEnabled false
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

growingAutotracker {
    development true
    logEnabled true
//    excludePackages "com.gio.test.three.autotrack.fragments", "com.example"
}
def rootDir = project.rootProject.rootDir

def coverageSourceDirs = [
        "$rootDir/growingio-tracker/src/main/java",
        "$rootDir/growingio-autotracker/src/main/java",
        "$rootDir/growingio-autotracker-gradle-plugin/src/main/java"
]

def coverageClassDirs = [
        "$rootDir/growingio-tracker/build/intermediates/javac/debug/classes",
        "$rootDir/growingio-autotracker/build/intermediates/javac/debug/classes",
        "$rootDir/growingio-autotracker-gradle-plugin/build/classes/java/main"
]

task jacocoTestReport(type: JacocoReport, group: 'Reporting', dependsOn: 'connectedCheck') {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    sourceDirectories = files(coverageSourceDirs)

    executionData = fileTree(
            dir: "$rootDir",
            include: ["**/*.exec", "**/*.ec"]
    )

    doFirst {
        println(project.rootProject.rootDir)
        classDirectories = files(files(coverageClassDirs).collect {
            fileTree(dir: it, exclude: [
                    'android/**',
                    'androidx/**',
                    'com/example/**',
                    'com/gio/**',
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
            ])
        })
    }
}

dependencies {
    implementation libraries.android.appcompat
    implementation libraries.android.constraint
    androidTestUtil libraries.test.orchestrator
    androidTestImplementation libraries.test.androidx_junit
    androidTestImplementation libraries.test.androidx_runner
    androidTestImplementation libraries.test.espresso_core
    androidTestImplementation libraries.test.androidx_rules
    androidTestImplementation libraries.test.mockito_core
    androidTestImplementation libraries.test.truth
    androidTestImplementation libraries.test.okhttp3_mockwebserver
    androidTestImplementation(libraries.test.awaitility) {
        exclude group: 'org.hamcrest', module: 'hamcrest'
    }
    androidTestImplementation libraries.test.powermock_reflect
    androidTestImplementation libraries.androidx.appcompat
    androidTestImplementation libraries.android.appcompat

    debugImplementation project(':growingio-tracker')
    releaseImplementation "com.growingio.android:tracker:$releaseConfiguration.releaseVersion"
    implementation project(':demo-core')
    if (withAutotrack) {
        implementation project(":demo-autotrack")
    }

    compileOnly libraries.androidx.appcompat
}
