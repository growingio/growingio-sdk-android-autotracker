// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildConfiguration = [
                compileVersion     : 33,
                minSdkVersion      : 17,
                targetSdkVersion   : 33,
                sourceCompatibility: JavaVersion.VERSION_1_8,
                targetCompatibility: JavaVersion.VERSION_1_8,
        ]
    }

}

plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false
    alias libs.plugins.kotlin.jvm apply false

    alias libs.plugins.nexus.staging apply false
    alias libs.plugins.sonarqube apply false

    alias libs.plugins.protobuf apply false

    alias libs.plugins.growingio.plugin apply false

    alias libs.plugins.spotless apply false
}

apply from: "${rootProject.projectDir}/gradle/sonarcloud.gradle"
apply from: "${rootProject.projectDir}/gradle/stagingMaven.gradle"

allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            // allWarningsAsErrors = true

            // freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
            // Set JVM target to 8
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}

//./gradlew spotlessCheck  ==> check code
//./gradlew spotlessApply  ==> format code
subprojects {
    apply plugin: "com.diffplug.spotless"
    spotless {
        java {
            target '**/*.java'
            licenseHeaderFile rootProject.file('gradle/java.header')

            removeUnusedImports()
        }
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")

            ktlint()
            licenseHeaderFile rootProject.file('gradle/java.header')
        }
        groovyGradle {
            target '*.gradle'
            greclipse()
        }

        format 'xml', {
            target '**/*.xml'
        }
    }
}
