plugins {
	id 'com.android.library'
	id 'com.google.protobuf'
}

android {
	namespace 'com.growingio.android.protobuf'
	compileSdkVersion buildConfiguration.compileVersion
	defaultConfig {
		minSdkVersion buildConfiguration.minSdkVersion
		targetSdkVersion buildConfiguration.targetSdkVersion
		versionName libs.versions.growingio.get()
		versionCode libs.versions.growingioCode.get().toInteger()
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			consumerProguardFiles 'consumer-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility buildConfiguration.sourceCompatibility
		targetCompatibility buildConfiguration.targetCompatibility
	}

	testOptions {
		unitTests.all {
			jacoco {
				includeNoLocationClasses = true
				excludes = ['jdk.internal.*']
			}
		}
		unitTests {
			returnDefaultValues = true
			includeAndroidResources = true
		}
	}

	sourceSets {
		main {
			java {
				srcDir 'src/main/java'
			}
			proto {
				srcDir 'src/main/proto'
				include '**/*.proto'
			}
		}
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${libs.versions.protobuf.get()}"
	}
	generateProtoTasks {
		all().each { task ->
			task.builtins {
				java { option "lite" }
			}
		}
	}
}

dependencies {
	testImplementation libs.bundles.test
	testImplementation libs.androidx.test.core

	implementation libs.protobuf.javalite

	implementation project(':growingio-tracker-core')

	implementation project(":growingio-annotation")
	annotationProcessor project(":growingio-annotation:compiler")
}
apply from: "${rootProject.projectDir}/gradle/publishMaven.gradle"
apply from: "${rootProject.projectDir}/gradle/jacocoModule.gradle"