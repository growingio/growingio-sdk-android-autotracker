plugins {
    id 'com.android.library'
    id 'com.google.protobuf'
}

android {
    compileSdkVersion buildConfiguration.compileVersion
    defaultConfig {
        minSdkVersion buildConfiguration.minSdkVersion
        targetSdkVersion buildConfiguration.targetSdkVersion
        versionName releaseConfiguration.releaseVersion
        versionCode releaseConfiguration.releaseVersionCode
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'consumer-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility buildConfiguration.sourceCompatibility
        targetCompatibility buildConfiguration.targetCompatibility
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            proto {
                srcDir 'src/main/proto'
                include '**/*.proto'
            }
        }
    }
}

protobuf {
    protoc {
        artifact = libraries.google.protobufc
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option "lite" }
            }
        }
    }
}

dependencies {
    testImplementation libraries.test.junit
    testImplementation libraries.test.truth
    testImplementation libraries.test.androidx_core
    testImplementation libraries.test.robolectric

    implementation libraries.google.protobuf_javalite

    implementation project(':growingio-tracker-core')

    compileOnly project(':growingio-advert')

    implementation project(":growingio-annotation")
    annotationProcessor project(":growingio-annotation:compiler")
}
apply from: "${rootProject.projectDir}/gradle/publishMaven.gradle"
apply from: "${rootProject.projectDir}/gradle/jacocoModule.gradle"