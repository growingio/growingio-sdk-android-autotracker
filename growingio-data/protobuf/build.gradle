plugins {
	alias libs.plugins.android.library
	id 'com.google.protobuf'
}

android {
	namespace 'com.growingio.android.protobuf'
	compileSdk libs.getVersions().getCompileSdk().get().toInteger()
	defaultConfig {
		minSdk libs.getVersions().getMinSdk().get().toInteger()
		versionName libs.versions.growingio.get()
		versionCode libs.versions.growingioCode.get().toInteger()
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			consumerProguardFiles 'consumer-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	testOptions {
		unitTests.all {
			jacoco {
				includeNoLocationClasses = true
				excludes = ['jdk.internal.*']
			}
		}
		unitTests {
			returnDefaultValues = true
			includeAndroidResources = true
		}
	}

	sourceSets {
		main {
			java {
				srcDir 'src/main/java'
			}
			proto {
				srcDir 'src/main/proto'
				include '**/*.proto'
			}
		}
	}
}

protobuf {
	protoc {
		// You still need protoc like in the non-Android case
		artifact = 'com.google.protobuf:protoc:3.7.0'
	}
	plugins {
		javalite {
			// The codegen for lite comes as a separate artifact
			artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
		}
	}
	generateProtoTasks {
		all().configureEach { task ->
			task.builtins {
				// In most cases you don't need the full Java output
				// if you use the lite output.
				remove java
			}
			task.plugins {
				javalite { }
			}
		}
	}
}

dependencies {
	testImplementation libs.bundles.test
	testImplementation libs.androidx.test.core

//	implementation libs.protobuf.javalite
	implementation 'com.google.protobuf:protobuf-lite:3.0.0'

	implementation project(':growingio-tracker-core')

	implementation project(":growingio-annotation")
	annotationProcessor project(":growingio-annotation:compiler")
}
apply from: "${rootProject.projectDir}/gradle/publishMaven.gradle"
apply from: "${rootProject.projectDir}/gradle/jacocoModule.gradle"